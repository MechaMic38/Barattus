buildscript {
    repositories {
        gradlePluginPortal()
    }

    dependencies {
        classpath "org.openjfx:javafx-plugin:$JAVAFX_PLUGIN_VERSION"
        classpath "gradle.plugin.com.github.johnrengelman:shadow:7.1.2"
    }
}

allprojects {
    group 'com.mechamic38.barattus'
    version BARATTUS_VERSION

    apply plugin: "com.github.johnrengelman.shadow"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.openjfx.javafxplugin'
    apply from: "$rootProject.projectDir/dependencies.gradle"

    repositories {
        mavenCentral()
    }

    [compileJava, compileTestJava].forEach {
        it.sourceCompatibility = JAVA_VERSION
        it.targetCompatibility = JAVA_VERSION
        it.options.encoding = 'UTF-8'
    }

    test {
        useJUnitPlatform()
    }

    javafx {
        version = JAVAFX_VERSION
    }

    dependencies {
        implementation libs.annotations
        implementation libs.logger

        testImplementation libsTest.jupiterApi
        testImplementation libsTest.jupiterParams
        testRuntimeOnly libsTest.jupiterEngine
    }
}